// https://backtony.github.io/spring/2022-06-02-spring-module-1/
// https://www.youtube.com/watch?v=pSKY3-K9_qc
plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

subprojects {
    group 'com.programming.techie'
    version '1.0-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'java'
    // build.gradle에서 api() 를 사용하려면 java-library 사용
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply from: "$rootProject.projectDir/dependencies.gradle"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://artifactory-oss.prod.netflix.net/artifactory/maven-oss-candidates' }
    }


    dependencies {
        implementation platform(lib.testContainersBom) //import bom
        implementation platform(lib.springCloudBom) //import bom
        implementation lib.springStarter

        compileOnly lib.lombok
        annotationProcessor lib.lombok

        testImplementation libsTest.jupiterContainersTest
        testImplementation libsTest.springStarterTest
    }


    test {
        useJUnitPlatform()
    }

}


